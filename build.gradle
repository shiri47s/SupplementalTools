plugins {
    id 'dev.architectury.loom' version '1.7-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id "com.matthewprenger.cursegradle" version "1.4.0" apply false
    id "com.modrinth.minotaur" version "2.+" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        //mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
        mappings loom.layered {
            it.mappings("net.fabricmc:yarn:$rootProject.yarn_mappings:v2")
            //it.mappings("dev.architectury:yarn-mappings-patch-neoforge:$rootProject.neoforge_patch")
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_name
    version = rootProject.mod_version + "_" + rootProject.minecraft_version + "-" + project.name
    group = rootProject.maven_group

    repositories {
        maven { url = "https://maven.shedaniel.me/" }
        maven { url = "https://maven.terraformersmc.com/" }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
}

subprojects {
    if (!enabled_platforms.split(',').contains(project.name))
        return

    def RELEASE = project.release_type

    def CHANGE_LOG = rootProject.file('changelog.md').getText("UTF-8")
    def PUBLISH_NAME = "${project.archives_name}-v${project.mod_version}_${project.minecraft_version}-${project.name}"
    def MOD_LOADER = project.name

    apply plugin: "com.modrinth.minotaur"
    modrinth {
        def MODRINTH_TOKEN = System.getenv("MODRINTH_TOKEN")
        if (MODRINTH_TOKEN != null) {
            token = MODRINTH_TOKEN
            projectId = project.modrinth_id
            versionName = PUBLISH_NAME
            versionNumber = project.mod_version
            versionType = RELEASE
            uploadFile = remapJar
            changelog = CHANGE_LOG
            gameVersions = ["${project.minecraft_version}"]
            loaders = [MOD_LOADER]
            dependencies {
                required.project "fabric-api"
                required.project "architectury-api"
            }
        }
    }

    apply plugin: "com.matthewprenger.cursegradle"
    curseforge {
        def CURSEFORGE_TOKEN = System.getenv("CURSEFORGE_TOKEN")
        if (CURSEFORGE_TOKEN != null) {
            apiKey = CURSEFORGE_TOKEN
            //noinspection GroovyAssignabilityCheck
            project {
                id = project.curseforge_id
                releaseType = RELEASE
                changelogType = "markdown"
                changelog = CHANGE_LOG
                addGameVersion project.support_versions

                if (MOD_LOADER.contains("fabric")) {
                    relations {
                        requiredDependency 'fabric-api'
                        requiredDependency 'architectury-api'
                    }
                }
                else {
                    relations {
                        requiredDependency 'architectury-api'
                    }
                }

                mainArtifact(remapJar.archiveFile.get().asFile) {
                    displayName = PUBLISH_NAME
                }
                afterEvaluate {
                    uploadTask.dependsOn("build")
                }
            }
            options {
                forgeGradleIntegration = false
                javaVersionAutoDetect = true
            }
        }
    }
}
